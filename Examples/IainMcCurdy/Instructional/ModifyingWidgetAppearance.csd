ModifyingWidgetAppearance.csd
Written by Iain McCurdy, 2014

An example that demonsstrates the ability to modify various attributes of a widget's appearance in realtime from within the Csound orchestra via Cabbage's own widgets.

<Cabbage>
form caption("Modifying Widget Appearance") size(800, 400), pluginID("TEx1"), guirefresh(10)
image bounds(20,180,11,11), colour(250,250,  0,255), shape("ellipse"), outlinecolour("white"), line(0), identchannel("image1")
hslider bounds(  0, 210, 450, 30), channel("HorPos"),  range(  0,450, 20), fontcolour(white) colour(SlateGrey)
label      bounds(210,233, 35, 11), text("X Pos."), fontcolour("white")
vslider bounds(460,  0, 30, 200),  channel("VertPos"), range(  0,200, 20), fontcolour(white) colour(SlateGrey)
label      bounds(484,100, 35, 11), text("Y Pos."), fontcolour("white")
hslider bounds( 10, 240, 200, 30), channel("SizeX"),   range(  1,200, 11), fontcolour(white) colour(SlateGrey), text("Size X")
hslider bounds( 10, 270, 200, 30), channel("SizeY"),   range(  1,200, 11), fontcolour(white) colour(SlateGrey), text("Size Y")
hslider bounds(260, 240, 200, 30), channel("Red"),     range(  0,255,250), fontcolour(red)   colour(red),   text("Red"),  trackercolour(red)
hslider bounds(260, 270, 200, 30), channel("Green"),   range(  0,255,250), fontcolour(green) colour(green), text("Green"),  trackercolour(green)
hslider bounds(260, 300, 200, 30), channel("Blue"),    range(  0,255,  0), fontcolour(blue)  colour(blue),  text("Blue"),  trackercolour(blue)
hslider bounds(260, 330, 200, 30), channel("Alpha"),   range(  0,255,255), fontcolour(white) colour(SlateGrey), text("Alpha")

combobox   bounds( 10,310, 80, 20), channel("Shape"), items("rounded", "sharp", "ellipse"), value(3), channeltype("string")

hslider bounds( 10, 340, 200, 30), channel("Line"), range(0, 10, 0), fontcolour(white) colour(SlateGrey), text("Line")


image bounds(525,10,265,350), colour( 10, 10, 15), shape("sharp"), outlinecolour("white"), line(0), plant("instructions"){
label      bounds(  5, 5, 255, 12),  fontcolour(white), align(left), text("It is possible to modify various aspects of a ")
label      bounds(  5,20, 255, 12),  fontcolour(white), align(left), text("widget's appearance during Csound  ")
label      bounds(  5,35, 255, 12),  fontcolour(white), align(left), text("performance. This is achieved through the ")
label      bounds(  5,50, 255, 12),  fontcolour(white), align(left), text("use of widget's 'identchannel' - a name given")
label      bounds(  5,65, 255, 12),  fontcolour(white), align(left), text("to the widget to identify it from within the ")
label      bounds(  5,80, 255, 12),  fontcolour(white), align(left), text("Csound orchestra. The widget is sent an ")
label      bounds(  5,95, 255, 12),  fontcolour(white), align(left), text("instruction as a string variable  ")
label      bounds(  5,110, 255, 12), fontcolour(white), align(left), text("corresponding to the change in appearance")
label      bounds(  5,125, 255, 12), fontcolour(white), align(left), text("we desire. This message is sent using the)
label      bounds(  5,140, 255, 12), fontcolour(white), align(left), text("'chnset' opcode and the channel used is the ")
label      bounds(  5,155, 255, 12), fontcolour(white), align(left), text("'identchannel' name we defined previously ")
label      bounds(  5,170, 255, 12), fontcolour(white), align(left), text("for the widget.")
label      bounds(  5,190, 255, 12), fontcolour(white), align(left), text("In this example the position and ")
label      bounds(  5,205, 255, 12), fontcolour(white), align(left), text("dimensions of an 'image' widget [the ")
label      bounds(  5,220, 255, 12), fontcolour(white), align(left), text("yellow circle] can be modified using the")
label      bounds(  5,235, 255, 12), fontcolour(white), align(left), text("on-screen sliders.")
label      bounds(  5,255, 255, 12), fontcolour(white), align(left), text("For the sake of efficiency widgets are only")
label      bounds(  5,270, 255, 12), fontcolour(white), align(left), text("sent messages when the sliders are moved.")
label      bounds(  5,285, 255, 12), fontcolour(white), align(left), text("We can detect changes in the sliders using")
label      bounds(  5,300, 255, 12), fontcolour(white), align(left), text("triggers generated by the 'changed' opcode.")
label      bounds(  5,315, 255, 12), fontcolour(white), align(left), text("")
label      bounds(  5,330, 255, 12), fontcolour(white), align(left), text("")
}

</Cabbage>

<CsoundSynthesizer>

<CsOptions>
-d -n
</CsOptions>

<CsInstruments>

sr 		= 	44100	;SAMPLE RATE
ksmps 		= 	32	;NUMBER OF AUDIO SAMPLES IN EACH CONTROL CYCLE
nchnls 		= 	2	;NUMBER OF CHANNELS (2=STEREO)
0dbfs		=	1

instr	1

 ; Change size and position (bounds)
 kHorPos		chnget	"HorPos"
 kVertPos	chnget	"VertPos"
 kSizeX		chnget	"SizeX"
 kSizeY		chnget	"SizeY"
 if changed(int(kHorPos))==1||changed(int(kVertPos))==1||changed(int(kSizeX))==1||changed(int(kSizeY))==1 then
   Smessage sprintfk "bounds(%d,%d,%d,%d)", kHorPos, 200-kVertPos, kSizeX, kSizeY
   chnset Smessage, "image1"
 endif
 
 ; Change shape
 SShape	chnget	"Shape"
 if changed(SShape)==1 then
  Smessage	sprintfk	"shape(%s)", SShape
  chnset	Smessage, "image1"
 endif
 
 ; Change line
 kLine	chnget	"Line"
 if changed(kLine)==1 then
   Smessage	sprintfk	"line(%d)",kLine
   chnset Smessage, "image1"
 endif

 ; Change colour
 kRed		chnget	"Red"
 kGreen		chnget	"Green"
 kBlue		chnget	"Blue"
 kAlpha		chnget	"Alpha" 
 if changed(int(kRed))==1||changed(int(kGreen))==1||changed(int(kBlue))==1||changed(int(kAlpha))==1 then
  Smessage sprintfk "colour(%d,%d,%d,%d)", kRed, kGreen, kBlue, kAlpha
  chnset Smessage, "image1"
 endif

endin
	
</CsInstruments>

<CsScore>
i 1 0 [3600*24*7]
</CsScore>

</CsoundSynthesizer>